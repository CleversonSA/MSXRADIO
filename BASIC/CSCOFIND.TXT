10 REM <<<< IDENTIFICADOR DE INDICATIVO >>>>
20 REM <<<< AUTOR: CLEVERSON SA >>>>
30 REM <<<< O FUNCIONAMENTO DESSE PROGRAMA >>>>
40 REM <<<< DEPENDE DOS ARQUIVOS IARU_X.DAT >>>>
50 REM <<<< QUE SAO AS BASES DE DADOS >>>>
60 CLS:SCREEN 0:KEY OFF
65 REM   "1234567890123456789012345678901234567"
70 PRINT "*** CONSULTAR PAIS DO INDICATIVO  ***"
75 PRINT:PRINT"Entre com o indicativo a pesquisar"
80 CS$="":INPUT CS$:GOSUB 1500
90 IF ER$<>"" THEN 100 ELSE 120
100 PRINT:PRINT "**** ATENCAO ****":PRINT:PRINT ER$:PRINT
110 R$=INPUT$(1):GOTO 60
120 GOSUB 1030
210 IF ER$<>"" THEN 220 ELSE 230
220 PRINT:PRINT "**** ATENCAO ****":PRINT:PRINT ER$:PRINT:R$=INPUT$(1):GOTO 60
230 IF PA$<>"" THEN 240 ELSE 300
240 PRINT:PRINT "O INDICATIVO [";CS$;"] PARECE SER DO PAIS:":PRINT:PRINT PA$:PRINT
250 BEEP:PRINT "Nova consulta (S/N)?":R$=INPUT$(1)
260 IF R$="S" OR R$="s" THEN RUN ELSE 310
300 PRINT:PRINT "PAIS DO INDICATIVO NAO LOCALIZADO!":PRINT:GOTO 250
310 CLS:LOAD "RADIO.BAS",R
1000 REM *************************
1010 REM BANCO DE DADOS
1020 REM *************************
1030 ID$="":DB$="":ER$="":PA$=""
1040 ID$=MID$(CS$,1,1):DB$="IARU_"+ID$+".DAT":CF$=MID$(CS$,1,2)
1050 REM ON ERROR GOTO 1200
1060 OPEN DB$ FOR INPUT AS #1
1070 REM ON ERROR GOTO 1210
1080 IF EOF(1) THEN 1300
1090 LINE INPUT #1,RE$
1100 C1$=MID$(RE$,1,2):C2$=MID$(RE$,3,61)
1110 IF C1$=CF$ THEN 1130
1120 RE$="":C1$="":C2$="":GOTO 1080: REM <<<< LIMPE A VAR DE STR ANTES
1130 RE$="":C1$="":PA$=C2$
1140 GOTO 1300
1200 ER$="ERRO AO ABRIR O BANCO DE DADOS!":RETURN
1210 ER$="ERRO AO LER O BANCO DE DADOS!":CLOSE #1:RETURN
1300 CLOSE #1:RETURN:REM <<<< PA PAIS ENCONTRADO
1500 REM *************************
1510 REM VALIDACAO INDICATIVO
1520 REM *************************
1530 ER$="":T1$=""
1540 IF CS$="" THEN 1550 ELSE 1560
1550 ER$="INFORME UM INDICATIVO!":RETURN
1560 IF LEN(CS$)<3 OR LEN(CS$)>6 THEN 1570 ELSE 1580
1570 ER$="INDICATIVO INVALIDO!":RETURN
1580 FOR I=1 TO LEN(CS$)
1590 T1$=MID$(CS$,I,1)
1600 IF (ASC(T1$)<65 OR ASC(T1$)>90) AND NOT (ASC(T1$)>=48 AND ASC(T1$)<=57) THEN 1610 ELSE 1620
1610 ER$="SOMENTE LETRAS MAIUSCULAS E NUMEROS SAO PERMITIDOS!":RETURN
1620 T1$="":NEXT I
1630 RETURN : REM <<<< PASSOU LISO
