10 REM <<<< CALCULADORA DISTANCIA ENTRE GRIDS >>>>
20 REM <<<< AUTOR: CLEVERSON SA >>>>
30 REM <<<< REF: https://contests.arrl.org/griddistancecalc.php >>>>
40 REM <<<< CONSTANTES
50 PI=3.141519265359#:DEF FNR(G)=G*(PI/180)
60 CT=6371.14:REM <<<< CURVATURA DA TERRA EM KM
70 REM ******************************
80 REM TELA
90 REM ******************************
100 CLS:SCREEN 0:KEY OFF
110 PRINT "*** CALCULADORA DIST. ENTRE GRIDS ***"
115 PRINT "-------------------------------------"
120 PRINT:PRINT   "Sao aceitos os grids nos formatos:"
130 PRINT"AA99   -> BAIXA PRECISAO"
140 PRINT"AA99bb -> ALTA PRECISAO"
150 PRINT
160 INPUT "GRID de Origem:";G1$
170 GS$=G1$:GOSUB 10420:IF ER$<>"" THEN 200 ELSE 180
180 INPUT "GRID de Destino:";G2$:PRINT
190 GS$=G2$:GOSUB 10420:IF ER$<>"" THEN 200 ELSE 220
200 PRINT:PRINT"!!! ATENCAO !!!":PRINT:PRINT ER$
210 PRINT:PRINT "Pressione qualquer tecla":RE$=INPUT$(1):GOTO 100
220 GS$=G1$:GOSUB 10000:L1=LA:O1=LO:B1$=BA$:B2$=BO$
230 GS$=G2$:GOSUB 10000:L2=LA:O2=LO:B3$=BA$:B4$=BO$
240 PRINT G1$;SPC(6-LEN(G1$));" LA ";:PRINT USING"###.### ";ABS(L1);:PRINT B1$;" LO ";:PRINT USING"###.### ";ABS(O1);:PRINT B2$
250 PRINT G2$;SPC(6-LEN(G2$));" LA ";:PRINT USING"###.### ";ABS(L2);:PRINT B3$;" LO ";:PRINT USING"###.### ";ABS(O2);:PRINT B4$
260 GOSUB 10320
270 BEEP:PRINT:PRINT USING"DISTANCIA APROX: ######.## KM";DS
280 PRINT:PRINT"Outra consulta? (S/N)"
290 RE$=INPUT$(1)
300 IF RE$="S" OR RE$="s" THEN GOTO 100
310 CLS:LOAD "RADIO.BAS",R: REM <<<< FIM DO PROGRAMA
9970 REM ****************************
9980 REM GRIDSQUARE - OBTEM LAT E LON
9990 REM ****************************
10000 LA=0:LO=0
10010 A2=ASC(MID$(GS$,2,1))
10020 A2=(A2-65)*10
10030 N4=INT(VAL(MID$(GS$,4,1)))
10040 LA=0!:IF LEN(GS$)>4 THEN GOSUB 10200 ELSE LA=-90
10050 LA=LA+A2+N4
10060 A1=ASC(MID$(GS$,1,1))
10070 A1=(A1-65)*20
10080 N3=INT(VAL(MID$(GS$,3,1)))*2
10090 LO=0!:IF LEN(GS$)>5 THEN GOSUB 10260
10100 LO=(LO+A1+N3)-180
10110 IF LA>0 THEN BA$="N" ELSE BA$="S"
10120 IF LO>0 THEN BO$="L" ELSE BO$="O"
10130 REM <<< LA LATITUDE, LO LONG, BA COORD N/S, BO COORD L/O
10140 RETURN
10200 REM ****************************
10210 REM PARA GRIDSQUARE COM 6 LETRAS
10220 REM ****************************
10230 A6=ASC(MID$(GS$,6,1))
10240 A6=(A6-97)/24:A6=A6+(1/48):A6=A6-90
10250 LA=A6:RETURN
10260 A5=ASC(MID$(GS$,5,1))
10270 A5=(A5-97)/12:A5=A5+(1/24)
10280 LO=A5:RETURN
10290 REM ***************************
10300 REM HAVERSINE
10310 REM ***************************
10320 R1=FNR(L1):R2=FNR(O1): REM <<<< RAD COORD 1
10330 R3=FNR(L2):R4=FNR(O2): REM <<<< RAD COORD 2
10340 DA=R3-R1:DO=R4-R2: REM <<< DELTA COORDS
10350 A=SIN(DA/2)^2+COS(R1)*COS(R3)*SIN(DO/2)^2
10360 C=2*ATN(SQR(A)/SQR(1-A))
10370 DS=CT*C: REM <<<< DS RESULTADO HAVERSINE
10380 RETURN
10390 REM ***************************
10400 REM VALIDA GRID LOCATOR
10410 REM ***************************
10420 REM <<<< E$ VARIAVEL ERRO
10430 ER$=""
10440 IF ER$="" AND LEN(GS$)>4 AND LEN(GS$)<6 THEN ER$="MAINHEAD INVALIDO!"
10450 IF ER$="" AND LEN(GS$)<4 THEN ER$="LOCATOR INCOMPLETO!"
10460 IF ER$<>"" THEN RETURN
10470 FOR I=1 TO 2
10480 IF ASC(MID$(GS$,I,1))<65 OR ASC(MID$(GS$,I,1))>90 THEN ER$="LOCATOR INVALIDO!"
10490 IF ER$<>"" THEN RETURN
10500 NEXT I
10510 FOR I=3 TO 4
10520 IF ASC(MID$(GS$,I,1))<48 OR ASC(MID$(GS$,I,1))>57 THEN ER$="LOCATOR INVALIDO!"
10530 IF ER$<>"" THEN RETURN
10540 NEXT I
10550 IF LEN(GS$)<6 THEN RETURN: REM <<<< SEM ERROS E SEM MAINHEAD
10560 FOR I=5 TO 6
10570 IF ASC(MID$(GS$,I,1))<97 OR ASC(MID$(GS$,I,1))>122 THEN ER$="LOCATOR INVALIDO!"
10580 IF ER$<>"" THEN RETURN
10590 NEXT I
10600 RETURN: REM <<<< SEM ERROS
